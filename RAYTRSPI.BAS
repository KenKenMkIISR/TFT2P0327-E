REM RAYTRACING (Reference I/O '86/7)
REM SPI-LCD TFT2P0327-E
REM CS:D9 RST:B0

USEVAR VERT
VERT=0:REM Horizontal Mode
SPI 8000
GOSUB LCDINI
SYSTEM 200,0
GOSUB LCDCLR
E=1:RESTORE DAT2
DIM K(9),D#(9,3),C(9),A(9),L#(2),V#(2),O#(2)
GOSUB DTREAD
FOR Y=64 TO -63 STEP -E
 FOR X=-80 TO 79 STEP E
  Z=160
  O#(0)=0:O#(1)=0:O#(2)=0
  B#=0:Q#=0:R#=1
  S=0
  W#=SQRT#(FLOAT#(X*X+Y*Y+Z*Z))
  V#(0)=FLOAT#(X)/W#
  V#(1)=FLOAT#(Y)/W#
  V#(2)=FLOAT#(Z)/W#

LABEL L2060
  W#=-1
  FOR I=0 TO P
   GOSUB CROSS,K(I),D#(I,0),D#(I,1),D#(I,2),D#(I,3),V#(0),V#(1),V#(2)
   IF INT(T#>0) AND A(I)>=0 THEN
    GOSUB ANDSUB,I,V#(0),V#(1),V#(2)
    IF INT(T#<0) AND K(I)=1 THEN
     T#=U#
     GOSUB ANDSUB,I,V#(0),V#(1),V#(2)
    ENDIF
   ENDIF
   IF T#>0 THEN
    IF W#=-1 OR T#<W# THEN
     W#=T#
     M=I
    ENDIF
   ENDIF
  NEXT

  IF W#<0 AND R#<1 THEN JPUT
  IF W#<0 THEN CONTINUE
  O#(0)=V#(0)*W#+O#(0)
  O#(1)=V#(1)*W#+O#(1)
  O#(2)=V#(2)*W#+O#(2)
  T#=-1
  FOR I=0 TO P
   IF I=M THEN CONTINUE
   GOSUB CROSS,K(I),D#(I,0),D#(I,1),D#(I,2),D#(I,3),L#(0),L#(1),L#(2)
   IF INT(T#>0) AND A(I)>=0 THEN
    GOSUB ANDSUB,I,L#(0),L#(1),L#(2)
    IF T#>0 THEN BREAK
    IF K(I)=1 THEN
     T#=U#
     GOSUB ANDSUB,I,L#(0),L#(1),L#(2)
    ENDIF
   ENDIF
   IF T#>0 THEN BREAK
  NEXT
  IF INT(T#>0) AND C(M)!=-1 THEN B#=0.125:GOTO CHECK
  GOSUB BRIGHT
  IF C(M)!=-1 THEN NOTMIR
  IF T#<=0 THEN
   IF U#>0.85 THEN Q#=Q#+POW#(U#,20)*1.5
  ENDIF
  Q#=Q#+B#*0.1+0.0156
  IF R#<=0.125 THEN CONTINUE
  R#=R#-0.125
  GOTO L2060

LABEL NOTMIR
  IF U#>0.75 THEN Q#=Q#+POW#(U#,10)
  IF B#<0.125 THEN B#=0.125

LABEL CHECK
  IF C(M)!=0 THEN
   S=C(M)
  ELSE
   IF INT(FLOOR#(O#(0)/F#)+FLOOR#(O#(1)/F#)+FLOOR#(O#(2)/F#)) AND 1 THEN S=H ELSE S=G
  ENDIF

LABEL JPUT
  GOSUB PUTDOT,X+80,64-Y
 NEXT
NEXT
SYSTEM 200,1
END

LABEL PUTDOT
VAR I,J,X,Y,A,C
X=ARGS(1):Y=ARGS(2)
FOR J=0 TO E-1
 FOR I=0 TO E-1
  IF FLOAT#(RND())/32768<Q# THEN
   GOSUB LCDDOT,X+I,Y+J,$FFFF
  ELSE
   A=INT(B#*R#*31+FLOAT#(RND())/32768)
   C=0
   IF S AND 2 THEN C=A<<11
   IF S AND 4 THEN C=C+(A<<6)
   IF S AND 1 THEN C=C+A
   GOSUB LCDDOT,X+I,Y+J,C
  ENDIF
 NEXT
NEXT
RETURN

LABEL CROSS
VAR A#,B#,C#,D#,L#,M#,N#
VAR P#,Q#
A#=ARGS#(2)
B#=ARGS#(3)
C#=ARGS#(4)
D#=ARGS#(5)
L#=ARGS#(6)
M#=ARGS#(7)
N#=ARGS#(8)
IF ARGS(1)=1 THEN
 A#=O#(0)-A#:B#=O#(1)-B#:C#=O#(2)-C#
 P#=A#*L#+B#*M#+C#*N#
 Q#=P#*P#-A#*A#-B#*B#-C#*C#+D#*D#
 IF Q#<0 THEN
  T#=-1
 ELSE
  T#=SQRT#(Q#)
  U#=0-P#+T#
  T#=0-P#-T#
 ENDIF
ELSE
 P#=A#*L#+B#*M#+C#*N#
 IF P#=0 THEN
  T#=-1
 ELSE
  T#=(D#-O#(0)*A#-O#(1)*B#-O#(2)*C#)/P#
 ENDIF
ENDIF
RETURN

LABEL ANDSUB
VAR X#,Y#,Z#,F#,G#,I,J
I=ARGS(1)
X#=ARGS#(2)*T#+O#(0):Y#=ARGS#(3)*T#+O#(1):Z#=ARGS#(4)*T#+O#(2)
J=A(I)
DO WHILE J!=I
 IF K(J)=1 THEN
  F#=X#-D#(J,0):G#=F#*F#
  F#=Y#-D#(J,1):G#=F#*F#+G#
  F#=Z#-D#(J,2):G#=F#*F#+G#
  F#=D#(J,3):G#=G#-F#*F#
  IF G#>0 THEN T#=-1
 ELSE
  IF D#(J,0)*X#+D#(J,1)*Y#+D#(J,2)*Z#>D#(J,3) THEN T#=-1
 ENDIF
 IF T#=-1 THEN BREAK
 J=A(J)
LOOP
RETURN

LABEL BRIGHT
VAR X#,Y#,Z#,T#
IF K(M)=1 THEN
 X#=O#(0)-D#(M,0)
 Y#=O#(1)-D#(M,1)
 Z#=O#(2)-D#(M,2)
 T#=SQRT#(X#*X#+Y#*Y#+Z#*Z#)
 X#=X#/T#:Y#=Y#/T#:Z#=Z#/T#
ELSE
 X#=D#(M,0):Y#=D#(M,1):Z#=D#(M,2)
ENDIF
B#=L#(0)*X#+L#(1)*Y#+L#(2)*Z#
IF V#(0)*X#+V#(1)*Y#+V#(2)*Z#>0 THEN B#=0-B#
IF B#<0 THEN B#=0
T#=2*(V#(0)*X#+V#(1)*Y#+V#(2)*Z#)
V#(0)=V#(0)-T#*X#
V#(1)=V#(1)-T#*Y#
V#(2)=V#(2)-T#*Z#
U#=V#(0)*L#(0)+V#(1)*L#(1)+V#(2)*L#(2)
RETURN

LABEL DTREAD
VAR I,J,N,X#,Y#,Z#,T#
L#(0)=VAL#(READ$())
L#(1)=VAL#(READ$())
L#(2)=VAL#(READ$())
F#=VAL#(READ$())
G=READ():H=READ()
I=0:J=-1
WHILE 1
 N=READ()
 IF N=0 THEN
  IF J>=0 THEN
   BREAK
  ELSE
   J=I-1:CONTINUE
  ENDIF
 ENDIF
 K(I)=N
 X#=FLOAT#(READ())
 Y#=FLOAT#(READ())
 Z#=FLOAT#(READ())
 D#(I,3)=FLOAT#(READ())
 C(I)=READ()
 A(I)=READ()-1
 IF K(I)=2 THEN
  T#=SQRT#(X#*X#+Y#*Y#+Z#*Z#)
  X#=X#/T#:Y#=Y#/T#:Z#=Z#/T#
  D#(I,3)=D#(I,3)/T#
 ENDIF
 D#(I,0)=X#
 D#(I,1)=Y#
 D#(I,2)=Z#
 I=I+1
WEND
P=J
RETURN

LABEL LCDDOT
 REM Set LCD Pixel Color
 GOSUB LCDXY,ARGS(1),ARGS(2)
 GOSUB LCDWRT,$22,ARGS(3)
RETURN

LABEL LCDXY
 REM SET LCD Cursor Position
 IF VERT THEN
  GOSUB LCDWRT,$21,(ARGS(2)<<8)+ARGS(1)
 ELSE
  GOSUB LCDWRT,$21,(ARGS(1)<<8)+ARGS(2)
 ENDIF
RETURN

LABEL LCDCLR
 REM Clear LCD
 GOSUB LCDXY,0,0
 GOSUB LCDIDX,$22
 FOR I=1 TO 20480:GOSUB LCDDAT,0:NEXT 
RETURN

LABEL LCDINI
 REM LCD Initialize
 OUT 0,0:WAIT 2:OUT 0,1:WAIT 1
 GOSUB LCDWRT,$0007,$0020
 GOSUB LCDWRT,$00B6,$013F
 GOSUB LCDWRT,$00B4,$0010
 GOSUB LCDWRT,$0012,$00B2
 GOSUB LCDWRT,$0013,$080E
 GOSUB LCDWRT,$0014,$5BCA
 GOSUB LCDWRT,$0061,$0018
 GOSUB LCDWRT,$0010,$190C

 WAIT 5

 GOSUB LCDWRT,$0013,$081E

 WAIT 2

 GOSUB LCDWRT,$0002,$0100

 IF VERT THEN
  REM In case of Vertical
  GOSUB LCDWRT,$0001,$0014
  GOSUB LCDWRT,$0003,$0030
 ELSE
  REM In case of Horizontal
  GOSUB LCDWRT,$0001,$0114
  GOSUB LCDWRT,$0003,$0028
 ENDIF

 GOSUB LCDWRT,$0008,$0202
 GOSUB LCDWRT,$000B,$0000
 GOSUB LCDWRT,$000C,$0000
 GOSUB LCDWRT,$0061,$0018
 GOSUB LCDWRT,$0069,$0000
 GOSUB LCDWRT,$0070,$0000
 GOSUB LCDWRT,$0071,$0000
 GOSUB LCDWRT,$0011,$0000

 GOSUB LCDWRT,$0030,$0303
 GOSUB LCDWRT,$0031,$0303
 GOSUB LCDWRT,$0032,$0303
 GOSUB LCDWRT,$0033,$0000
 GOSUB LCDWRT,$0034,$0404
 GOSUB LCDWRT,$0035,$0404
 GOSUB LCDWRT,$0036,$0404
 GOSUB LCDWRT,$0037,$0000
 GOSUB LCDWRT,$0038,$0707

 GOSUB LCDWRT,$0040,$0000
 GOSUB LCDWRT,$0042,$9F00
 GOSUB LCDWRT,$0043,$0000
 GOSUB LCDWRT,$0044,$7F00
 GOSUB LCDWRT,$0045,$9F00
 GOSUB LCDWRT,$0069,$0000
 GOSUB LCDWRT,$0070,$0000
 GOSUB LCDWRT,$0071,$0000
 GOSUB LCDWRT,$0073,$0000
 GOSUB LCDWRT,$00B3,$0000
 GOSUB LCDWRT,$00BD,$0000
 GOSUB LCDWRT,$00BE,$0000
 GOSUB LCDWRT,$0021,$0000

 WAIT 2

 GOSUB LCDWRT,$0007,$0020

 WAIT 2

 GOSUB LCDWRT,$0007,$0021
 GOSUB LCDWRT,$0007,$0027

 WAIT 4

 GOSUB LCDWRT,$0007,$0037
RETURN

LABEL LCDWRT
 GOSUB LCDIDX,ARGS(1)
 GOSUB LCDDAT,ARGS(2)
RETURN

LABEL LCDIDX
 SPIWRITE $70,ARGS(1)>>8,ARGS(1) AND $FF
RETURN

LABEL LCDDAT
 SPIWRITE $72,ARGS(1)>>8,ARGS(1) AND $FF
RETURN


LABEL DAT1
DATA "-0.57735"
DATA "0.57735"
DATA "-0.57735"
DATA "101"
DATA 4,5
DATA 1,  80,  0,400,  100,6,2
DATA 2,  -5,  3, -5,-2150,6,1
DATA 1,  80,  0,400,   60,2,0
DATA 1,-100,-50,400, 100,-1,0
DATA 2,   0,  0,  1,  520,0,0
DATA 0
DATA 0

LABEL DAT2
DATA "-0.57735"
DATA "0.57735"
DATA "-0.57735"
DATA "101"
DATA 4,5
DATA 1,   0,  73,715,100,-1,0
DATA 1,   0,-100,600,100, 6,0
DATA 1,-100,-100,733,100, 1,0
DATA 1, 100,-100,773,100, 2,0
DATA 2,   0,   1,  0,-200,0,0
DATA 0
DATA 0

LABEL DAT3
DATA "-0.57735"
DATA "0.57735"
DATA "-0.57735"
DATA "0"
DATA 0,0
DATA 1,80,0,400,100,5,2
DATA 0
DATA 2,-5,3,-5,-2150,5,1
DATA 0
